function loadItems():
  load all yaml from directory "/plugins/Items/"

  loop all of the currently loaded yaml files:
    set {_file} to loop-value-1
    set {_nodes::*} to all yaml nodes of {_file}

    loop {_nodes::*}:
      set {_node} to loop-value-2
      set {_index} to loop-index parsed as number
      set {_value} to yaml value loop-value-2 from {_file}
      
      # Проверяем...
      set {_temp::*} to {_node} split at "."
      set {_size} to size of {_temp::*}
      {_temp::%{_size}%} is "type"
      {_value} is "item":
        set {_node::previous} to {_index} - 1
        loadItemNode({_nodes::%{_node::previous}%}, {_file})

# Функция, которая займётся загрузкой одного предмета
# (нужно передать файл, в котором этот предмет находится
# и местонахождение главной ноды)
function loadItemNode(node: string, file: string):
  # Подготавливаем ИД предмета
  set {_temp::*} to {_node} split at "."
  set {_size} to size of {_temp::*}
  set {_id} to {_temp::%{_size}%}

  # Берём самые важные данные про этот предмет
  set {_item::id} to yaml value "%{_node}%.item" from {_file}

  # @visual
  set {_item::title} to yaml value "%{_node}%.visual.title" from {_file}
  if {_item::title} is not set:
    set {_item::title} to "Пустышка"
    
  set {_item::description::*} to yaml list "%{_node}%.visual.description" from {_file}
  if size of {_item::description::*} is 0:
    set {_item::description::*} to "Пустое описание"

  set {_item::nbt} to yaml value "%{_node}%.visual.nbt" from {_file}

  # @usedIn
  yaml list "%{_node}%.usedIn" from {_file} is set:
    set {_item::usedIn::*} to yaml list "%{_node}%.usedIn" from {_file}

    loop {_item::usedIn::*}:
      set {_moduleName} to loop-value
      evaluate:
        %{_moduleName}%_InitializeItem({_id})

  # @action
  set {_action::type} to yaml value "%{_node}%.action.type" from {_file}
  set {_action::function} to yaml value "%{_node}%.action.function" from {_file}

  # @createFunction
  set {_item::createFunction} to yaml value "%{_node}%.createFunction" from {_file}

  # @variables
  set {_action::variables::*} to yaml list "%{_node}%.action.variables" from {_file}

  {_action::type} is not set:
    set {_action::type} to "click"
  {_action::function} is not set:
    set {_action::function} to "AC_EmptyAction({_player})"

  {_id}, {_item::id} and {_item::title} is set:
    # Подготавливаем сам предмет
    evaluate:
      set {_item} to %{_item::id}% named "%{_item::title}%" with lore coloured "&0i/%{_id}%", "", {_item::description::*}

    if {_item} is not set:
      set {_item} to grass
    
    # Специальное: nbt
    if {_item::nbt} is set:
      add {_item::nbt} to item-nbt of {_item}

    # Загружаем сам предмет в базу данных
    set {data::items::%{_id}%::id} to {_id}
    set {data::items::%{_id}%::item} to {_item}
    set {data::items::%{_id}%::createFunction} to {_item::createFunction}
    set {data::items::%{_id}%::action::type} to {_action::type}
    set {data::items::%{_id}%::action::function} to {_action::function}

    # Переменные
    loop {_action::variables::*}:
      set {_temp::*} to loop-value split at "="

      set {_name} to {_temp::1}
      set {_value} to {_temp::2}

      set {data::items::%{_id}%::variable::%{_name}%} to {_value}
      add {_name} to {data::items::%{_id}%::variables::*}

    broadcast "[%{_id}%] Загружен предмет (ITEM ID: %{_item::id}%, TITLE: %{_item::title}%, ACTION TYPE: %{_action::type}%, ACTION FUNCTION: %{_action::function}%)"

    add {_id} to {data::items::list::*}

# Загружаем все предметы
# из файлов конфигурации
on load:
  delete {data::items::*}
  loadItems()

on place:
  set {_item} to player's held item
  set {_lore::*} to lore of {_item}
  if line 1 of {_item}'s lore contains "i/":
    set {_line} to {_lore::1}
    set {_temp::*} to {_line} split at "/"
    set {_id} to {_temp::2}

    # Берём информацию об предмете
    set {_action::type} to {data::items::%{_id}%::action::type}
    if {_action::type} is "place":
      # Вызываем функцию этого предмета
      set {_functionId} to "%player%-%random integer between 000 and 999%"

      # Настраиваем переменные
      set {function::%{_functionId}%::variables::player} to player
      set {function::%{_functionId}%::variables::location} to event-location
      set {function::%{_functionId}%::variables::block} to block

      # Сохраняем данные
      set {function::%{_functionId}%} to {data::items::%{_id}%::action::function}

      # Запускаем функцию
      evaluateFunction({_functionId})
    else:
      cancel event

on click:
  set {_item} to player's held item
  set {_lore::*} to lore of {_item}

  if first element of {_lore::*} contains "i/":
    set {_line} to first element of {_lore::*}
    set {_temp::*} to {_line} split at "/"
    set {_id} to {_temp::2}

    # Берём информацию об предмете
    set {_action::type} to {data::items::%{_id}%::action::type}
    if {_action::type} is "click":
      # Вызываем функцию этого предмета
      set {_functionId} to "%player%-%random integer between 000 and 999%"

      # Настраиваем переменные
      set {function::%{_functionId}%::variable::player} to player
      add "player" to {function::%{_functionId}%::variables::*}
      # set {function::%{_functionId}%::variables::location} to

      # Сохраняем данные
      set {function::%{_functionId}%} to {data::items::%{_id}%::action::function}
      set {function::%{_functionId}%::itemId} to {_id}

      # Запускаем функцию
      evaluateFunction({_functionId})
    else:
      cancel event