# 
# Функция, которая откроет терминал игроку
# и предложит ему вствить его кредитную карточку (если надо)
# 
function openTerminal(player: player, card: number):
  # 
  # Надо вставить кредитную карточку
  # в терминал.
  if {_card} is 0:
    open virtual chest inventory with size 3 named "&8&lТерминал - Вставьте карточку" to {_player}

    # Стрелочки
    set {_arrow::title} to "&fВставьте кредитную карточку"
    set {_arrow::description::*} to "", "&f &fДля того, что бы использовать терминал, вам", "&f &fнадо вставить в него вашу кредитную карточку.", "&f &fпросто положите вашу карточку сюда."

    set {_arrow::right} to player head named "&e %{_arrow::title}%"
    add "{SkullOwner:{Id:""e2944f18-e8a0-4ef0-823d-1a37b818bd92"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvOTU2YTM2MTg0NTllNDNiMjg3YjIyYjdlMjM1ZWM2OTk1OTQ1NDZjNmZjZDZkYzg0YmZjYTRjZjMwYWI5MzExIn19fQ==""}]}}}" to item-nbt of {_arrow::right}

    format gui slot 12 of {_player} with {_arrow::right} with lore {_arrow::description::*}

    set {_arrow::left} to player head named "&e %{_arrow::title}%"
    add "{SkullOwner:{Id:""0216fff0-74cd-4468-ac75-9614475231d9"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2RjOWU0ZGNmYTQyMjFhMWZhZGMxYjViMmIxMWQ4YmVlYjU3ODc5YWYxYzQyMzYyMTQyYmFlMWVkZDUifX19""}]}}}" to item-nbt of {_arrow::left}

    format gui slot 14 of {_player} with {_arrow::left} with lore {_arrow::description::*}

    set {temp::%{_player}%::menuAction} to "terminal"
  
  # 
  # Само окно терминала
  else:
    # Обновляем данные этого терминала
    open virtual chest inventory with size 6 named "&8&lТерминал" to {_player}

    # Пустые слоты
    set {_empty} to player head named "&f"
    add "{SkullOwner:{Id:""ee6cb849-1384-4efa-bed7-23ff26bf79b1"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYWQ5MzExN2I5ZTE4MGUwZGMzOWU1ZThhMDUwODQ4MmNmMWY2MGU0NDZlMDIyOTc4ZmUwNjUxYTU2MmE1OTdmIn19fQ==""}]}}}" to {_empty}'s nbt

    format gui slot 13, 21,22,23, 30,31,32, 39,40,41 of {_player} with {_empty}

    # Сама карта

    # Вывести деньги
    set {_withdraw} to player head named "&e△ &fВывести деньги" with lore "", "&f &fВыведите деньги из вашего банковского счета", "&f &fв два клика! Получите настоящие бумажные", "&f &fденьги чуть ли не моментально!", "", "&e▷ &fНажмите, то бы перейти"
    add "{SkullOwner:{Id:""8f54d1c4-c599-4c54-8993-2cb371649c33"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvODgyZmFmOWE1ODRjNGQ2NzZkNzMwYjIzZjg5NDJiYjk5N2ZhM2RhZDQ2ZDRmNjVlMjg4YzM5ZWI0NzFjZTcifX19""}]}}}" to item-nbt of {_withdraw}

    format gui slot 21 of {_player} with {_withdraw} to run:
      openWithdrawMenu({_player}, {_card})

    # Внести деньги
    set {_deposit} to player head named "&e▽ &fВнести деньги" with lore "", "&f &fВносите деньги в ваш банковый", "&f &fаккаунт на хранение. Потом вы сможете вывести", "&f &fих в любом банкомате ну или же использовать", "&f &fкарту в магазине с безконтактной оплатой.", "", "&e▷ &fНажмите, что бы перейти"
    add "{SkullOwner:{Id:""3554e03b-982d-44f1-8be4-71785ba822f8"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMWFkNmM4MWY4OTlhNzg1ZWNmMjZiZTFkYzQ4ZWFlMmJjZmU3NzdhODYyMzkwZjU3ODVlOTViZDgzYmQxNGQifX19""}]}}}" to item-nbt of {_deposit}

    format gui slot 22 of {_player} with {_deposit} to run:
      openDepositMenu({_player}, {_card})

    # Настройки карты
    set {_settings} to player head named "&7┅ &fНастройки карты" with lore "&7В разработке", "&f &fНастраивайте вашу карту так, как вам удобно.", "&f &fПоставить специальный пинкод, настроить Код Доступа", "&f &fдля интернет оплаты и настроить имя карты можно", "&f &fименно тут!", "", "&e▷ &fНажмите, что бы перейти"
    add "{SkullOwner:{Id:""fe358563-d1df-40f4-9bbf-e7e3c4a6ac22"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMWNiYTcyNzdmYzg5NWJmM2I2NzM2OTQxNTk4NjRiODMzNTFhNGQxNDcxN2U0NzZlYmRhMWMzYmYzOGZjZjM3In19fQ==""}]}}}" to item-nbt of {_settings}

    format gui slot 23 of {_player} with {_settings}

function openDepositMenu(player: player, card: number):
  set {temp::%{_player}%::depositMenu::cardId} to {_card}
  open virtual chest inventory with size 6 named "&8&lВнесите купюры" to {_player}

on inventory close:
  if name of player's current inventory contains "Внесите купюры":
    set {_id} to {temp::%player%::depositMenu::cardId}
    delete {temp::%player%::depositMenu::cardId}

    if {_id} is set:
      # Пополняем эту карту
      loop all items in player's current inventory:
        if name of loop-item contains "$":
          delete {_money}

          set {_quantity} to item amount of loop-item
          set {_money} to uncolored loop-item's name
          replace all "$" in {_money} with ""

          set {_money} to {_money} parsed as number

          if {_money} is set:
            add {_quantity} * {_money} to {_deposit::money}
          else:
            add loop-item to {_return::*}
        else:
          if name of loop-item doesn't contain "Помощь":
            add loop-item to {_return::*}

      # Обновляем кошелёк
      set {_balance} to getCardBalance({_id})
      setCardBalance({_id}, {_balance} + {_deposit::money})

      if size of {_return::*} > 0:
        loop {_return::*}:
          add loop-value to player's inventory

      if {temp::%{_player}%::blockTerminalViewAction} is set:
        delete {temp::%{_player}%::blockTerminalViewAction}
      send title "&e△ &fДеньги внесены" with subtitle "&7Вы внесли %{_deposit::money}%$ на баланс карточки." to {_player} for 4 seconds with 0 ticks fade in and 1 second fade out
      stop

    else:
      loop all items in player's current inventory:
        add loop-item to player's inventory

      sendNotification(player, "&c⚠ &fПроизошла ошибка", 6)

function openWithdrawMenu(player: player, card: number):
  close {_player}'s inventory

  set {_title} to "&e▽ &fВпишите в чат"

  set {_waiting} to true
  set {_balance} to getCardBalance({_card})
  if {_balance} is not set:
    set {_balance} to 100

  while {_waiting} is true:
    if {_player} is offline:
      stop

    if {temp::%{_player}%::blockTerminalViewAction} is not set:
      set {temp::%{_player}%::blockTerminalViewAction} to 5

    # Проверяем переменные
    if {temp::%{_player}%::chat::input} is not set:
      set {temp::%{_player}%::chat::input} to true

    if {temp::%{_player}%::chat::output} is set:
      delete {_output}
      set {_output} to {temp::%{_player}%::chat::output} parsed as number
      set {_output} to rounded {_output}

      delete {temp::%{_player}%::chat::output}

      if {_output} is set:
        if {_output} is 0:
          if {temp::%{_player}%::blockTerminalViewAction} is set:
            delete {temp::%{_player}%::blockTerminalViewAction}

          send title "&7" with subtitle "&7" to {_player}
          openTerminal({_player}, {_card})
          stop

        # Проверяем кое-что
        if {_output} <= {_balance}:
          # Выводим деньги
          if {_output} > 0:
            # Сначала сохраним новый счёт в банке
            setCardBalance({_card}, {_balance} - {_output})
            set {_money} to {_output}

            while {_output} > 0:
              if {_output} >= 100:
                set {_output} to {_output} - 100
                set {_item} to GetMoneyBillItem(100)
                add {_item} to {_items::*}
              else if {_output} >= 50:
                set {_output} to {_output} - 50
                set {_item} to GetMoneyBillItem(50)
                add {_item} to {_items::*}
              else if {_output} >= 20:
                set {_output} to {_output} - 20
                set {_item} to GetMoneyBillItem(20)
                add {_item} to {_items::*}
              else if {_output} >= 10:
                set {_output} to {_output} - 10
                set {_item} to GetMoneyBillItem(10)
                add {_item} to {_items::*}
              else if {_output} >= 5:
                set {_output} to {_output} - 5
                set {_item} to GetMoneyBillItem(5)
                add {_item} to {_items::*}
              else:
                set {_output} to {_output} - 1
                set {_item} to GetMoneyBillItem(1)
                add {_item} to {_items::*}

            loop {_items::*}:
              add loop-value to {_player}'s inventory

            # Визуал
            send title "&e$ &fДеньги получены" with subtitle "&7Вы успешно вывели %{_money}%$" to {_player} for 4 seconds with 0 ticks fade in and 1 second fade out
            
            if {temp::%{_player}%::blockTerminalViewAction} is set:
              set {temp::%{_player}%::blockTerminalViewAction} to 2
              
            delete {temp::%{_player}%::chat::*}
            stop
          else:
            # Просто откроем меню терминала
            if {temp::%{_player}%::blockTerminalViewAction} is set:
              delete {temp::%{_player}%::blockTerminalViewAction}

            send title "&7" with subtitle "&7" to {_player}
            openTerminal({_player}, {_card})
            stop
        else:
          set {_title} to "&c▽ &fНедостаточно средств"
      else:
        set {_title} to "&c▽ &fВпишите число"

    # Визуал
    send title "%{_title}%" with subtitle "&7Кол-во денег для вывода (Доступно: %{_balance}%$)" to {_player} for 2 seconds with 0 ticks fade in and 0 ticks fade out
    send action bar "&eⓘ &fНапишите &70 &fчто бы отменить транзакцию" to {_player}

    wait 1 tick

function loadTerminalViewWorkers(player: player):
  # Локации
  load yaml "/plugins/Terminals/config.yml" as "terminals-config"
  set {_locations::*} to yaml list "points" from "terminals-config"

  loop {_locations::*}:
    set {_location} to getLocation("%loop-value%", "world")
    set {_location} to location of block at {_location}

    set {workers::%{_player}%::viewWorker::block::%{_location}%::type} to "hologram"
    set {workers::%{_player}%::viewWorker::block::%{_location}%::hoverAction} to "TR_Holo_TerminalCreate"
    set {workers::%{_player}%::viewWorker::block::%{_location}%::clickAction} to "TR_Holo_TerminalClick"

# 
# Прослушиваем клики в инвентаре (терминал)
# 
on inventory click:
  if {temp::%player%::menuAction} is "terminal":
    if name of player's current inventory contains "Вставьте карточку":
      wait 2 ticks

      set {_item} to slot 13 of player's current inventory
      if "%{_item}%" contains "Карта":
        # Берём ид карточки и открываем меню
        set {_lore::*} to {_item}'s lore
        set {_id} to uncolored {_lore::1} parsed as number

        openTerminal(player, {_id})

on inventory close:
  if name of player's current inventory contains "Вставьте карточку":
    loop all items in player's current inventory:
      if name of loop-item doesn't contain "Вставьте":
        add loop-item to player's inventory