# 
# Запрещаем игроку делать то,
# что ему нельзя делать (пока он не авторизовался)
# 

on damage:
  if victim is player:
    if {authorized::%victim%} or {authorized::%attacker%} is not set:
      cancel event

on place:
  if {authorized::%player%} is not set:
    cancel event

on drop:
  if {authorized::%player%} is not set:
    cancel event

on pickup:
  if {authorized::%player%} is not set:
    cancel event

on break:
  if {authorized::%player%} is not set:
    cancel event

on inventory click:
  if {authorized::%player%} is not set:
    cancel event

on command:
  executor is player
  if {authorized::%player%} is not set:
    cancel event

on any move:
  if {authorized::%player%} is not set:
    cancel event

on quit:
  set leave message to ""

# 
# Телепорт игрока на точку авториазации
# и начало этой самой авторизации.
# 
on join:
  set join message to ""
  set {temp::%player%::currentSessionId} to random integer between 000 and 999

  if {data::hotbar::%player%::restore} is true:
    delete {data::hotbar::%player%::restore}

    set {_slot::current} to 0
    loop {data::hotbar::%player%::inventory::*}:
      set slot {_slot::current} of player's inventory to {data::hotbar::%player%::inventory::%{_slot::current}%}
      add 1 to {_slot::current}

  delete {authorized::%player%}
  delete {temp::%player%::*}

  # Телепортируем игрока на нужную локацию
  set {_location} to getLocation("-3942, 12, -2764", "world")
  teleport player to {_location}

  wait 1 tick

  # Сначала проверяем текущую сессию игрока
  if {auth::ip::%player%::token} is set:
    # Игрок уже авторизован:
    # теперь нам надо проверить текущее состояние сессии
    # и, если авторизация прошла более чем день назад,
    # запросить авторизацию со стороны игрока
    set {_player::time} to {auth::ip::%player%::authTime}
    if {_player::time} is not set:
      set {_player::time} to now
      add 2 days to {_player::time}

    set {_now} to now
    if difference between {_now} and {_player::time} >= 6 hours:
      # Удаляем всё самое ненужное
      delete {auth::ip::%player%::*}

      # И стартуем авторизацию заного
      StartAuthorization(player)
    else:
      AuthorizationDone(player)
  else:
    # Игроку нужно авторизоваться
    StartAuthorization(player)

on right click:
  if {authorized::%player%} is not set:
    if name of player's held item contains "веб-сайта":
      cancel event
      set {action::%player%::gui::am-01} to "web"

# 
# Функция, которая буквально начнёт
# процесс авторизации игрока. Игроку
# сразу же предложат авторизоваться
# или как гость или используя веб-ресурс
# 
function StartAuthorization(player: player):
  wait 2 ticks

  set {_slot::end} to 8
  set {_slot::current} to 0

  while {_slot::current} <= {_slot::end}:
    set {data::hotbar::%{_player}%::inventory::%{_slot::current}%} to slot {_slot::current} of {_player}'s inventory
    set slot {_slot::current} of {_player}'s inventory to air
    
    add 1 to {_slot::current}

  hide {_player} from all players
  hide all players from {_player}

  set {_player}'s gamemode to survival

  # Открываем игроку инвентарь с выбором
  # типа авторизации

    # веб-авторизация
  set {_web-authorization} to player head named "&9☁ &fАвторизоваться с помощью веб-сайта" with lore "", "&f &fАвторизоваться с помощью веб-ресурса &9Lococovu&f.", "&f &fАвторизовываясь именно через этот сайт вы получаете", "&f &fследующие преимущества:", "&f &f &9･ &fСохранение всего вашего игрового прогресса;", "&f &f &9･ &fСинхронизация вашего аккаунта вне зависимости", "&f &f &fот вашего ника", "&f &f &9･ &fДополнительную безопасность", "&f &f &9･ &fЕжедневные бонусы", "&f &f &9･ &fВозможность бесплатно запускать майнкрафт сервера", "&f &f &7... &fи многое другое", "", "&9 &fНажмите, что бы авторизоваться"
  add "{SkullOwner:{Id:""639c312e-b17d-40c3-8292-610ab0041d49"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMTA5Y2RlMWFmYzk1YTQ3NGQyMjI1NTQwOTdlZDZkMzkxZTdjYzdhZTFmMjAyZmRiZmQyZDZkYmM5ODMwOTM3MCJ9fX0=""}]}}}" to item-nbt of {_web-authorization}

  set slot 3 of {_player} to {_web-authorization}

    # гостевая авторизация
  set {_guest-authorization} to player head named "&7 &fАвторизоваться как Гость" with lore "&7В разработке", "&f &f"
  add "{SkullOwner:{Id:""af78fbcb-985a-4511-a13b-7ae2718d15ef"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvODJiMjJiZGFlNzk5MDgzYWQ4N2Q5ZDI1MWRlYzhiZDVkNTc0NGZiZTNiOGE1YmE0MDkxYmE1NTk3NGI4NWIifX19""}]}}}" to item-nbt of {_rooms::list}

  set slot 5 of {_player} to {_guest-authorization}

  # Запускаем вайтер и проверяем то,
  # что выбрал игрок (так же проверяем
  # факт того, закрыл ли игрок меню)
  set {_waiting} to true

  delete {action::%{_player}%::gui::am-01}

  while {_waiting} is true:
    if {_player} is offline:      
      set {data::hotbar::%{_player}%::restore} to true

      stop

    # Визуал
    send title "&e▽ &fАвторизация" with subtitle "&7Выберите самый удобный способ авторизации" to {_player} for 2 seconds with 0 ticks fade in and 1 second fade out

    if {action::%{_player}%::gui::am-01} is set:
      set {_type} to {action::%{_player}%::gui::am-01}
      delete {action::%{_player}%::gui::am-01}

      set {_waiting} to false
      close {_player}'s inventory

    wait 1 tick

  if {_type} is not set:
    set {_type} to "web"

  # Авторизация с помощью веб-ресурса
  if {_type} is "web":
    if {_player}'s gamemode isn't spectator:
      set {_player}'s gamemode to spectator

    # Давайте создадим ссылку для авторизации и "отошлём"
    # её игроку

    # %random integer between 0 and 9%
    set {_code} to "%random integer between 0 and 9%%random integer between 0 and 9%%random integer between 0 and 9%"

    # И теперь давайте запустим воркера,
    # который будет проверять эту ссылку.
    set {_working} to true
    set {_waiting::time} to 0

    set {_dots::times} to 1

    set {_request} to random integer between 10000 and 99999
    set {_request::timer} to 6

    while {_working} is true:
      if {_player} is offline:
        stop

      # Визуальная состовляющая
      send title "&9%first {_dots::times} characters of ""......""%" with subtitle "&7Перейдите по ссылке: &flococovu.me/&9%{_code}%" to {_player} for 2 seconds with 0 ticks fade in and 0 ticks fade out

      # Выполняем запрос на удалённый сервер
      if {_request::timer} is not set:
        add 1 to {_dots::times}
        set {_request::timer} to 3

        send web request to "https://v1.api.lococovu.me/api/callback/code/%{_code}%" and return as "%{_request}%"

      # Проверяем ответ удалённого сервера
      if {responses::%{_request}%::body} is set:
        delete {_body}
        delete {_response::*}

        set {_body} to {responses::%{_request}%::body}
        copy json {_body} to {_response::*}

        if {_response::error} isn't "NotFound":
          # И теперь давайте сохраним всю нужную
          # информацию про этот аккаунт.
          set {auth::ip::%{_player}%::uid} to "%{_response::pid}%"
          set {auth::ip::%{_player}%::code} to "%{_response::code}%"
          set {auth::ip::%{_player}%::token} to "%{_response::token}%"
          set {auth::ip::%{_player}%::authTime} to now

          # Проверяем, всё ли у нас стоит.
          
          if {auth::ip::%{_player}%::uid} or {auth::ip::%{_player}%::code} or {auth::ip::%{_player}%::token} is not set:
            StartAuthorization({_player})
            stop

          # И теперь вызываем функцию, которая
          # подтверждает авторизацию игрока.
          AuthorizationDone({_player})

          stop

      add 0.05 to {_waiting::time}
      if {_waiting::time} >= 1800:
        kick {_player} due to "&fЧувааак, ты не успел авторизоваться. У нас%newline% тут денег на серверное оборудывание-то нету,%newline% так что не занимай линию."
        stop

      # Визуальный таймер точек
      if {_dots::times} > 3:
        set {_dots::times} to 1

      # Таймер запроса
      if {_request::timer} <= 0:
        delete {_request::timer}
      else:
        reduce {_request::timer} by 0.05
      wait 1 tick
  else:
    if {_player}'s gamemode isn't survival:
      set {_player}'s gamemode to survival

# 
# Функция, которая означает что игрок закончил
# процесс авторизации.
# 
function AuthorizationDone(player: player):
  # Давайте сначала проверим - есть ли у нас
  # все самые нужные и важные данные о данном
  # аккаунте.
  set {_profile::uid} to getAccountID({_player})
  set {_profile::token} to {auth::ip::%{_player}%::token}

  # Проверяем
  if {_profile::uid} or {_profile::token} is not set:
    # Вызываем функцию авторизации
    StartAuthorization({_player})
    stop

  set {_slot::current} to 0
  loop {data::hotbar::%{_player}%::inventory::*}:
    set slot {_slot::current} of {_player}'s inventory to {data::hotbar::%{_player}%::inventory::%{_slot::current}%}
    add 1 to {_slot::current}

  set {_request} to random integer between 10000 and 99999

  set {_dots::times} to 1
  set {_working} to true

  # Берём информацию об аккаунте
  while {_working} is true:
    if {_player} is offline:
      stop

    # Визуальная состовляющая
    send title "&9%first {_dots::times} characters of ""......""%" with subtitle "&7Берём информацию об аккаунте..." to {_player} for 2 seconds with 0 ticks fade in and 0 ticks fade out

    # Выполняем запрос на удалённый сервер
    if {_request::timer} is not set:
      add 1 to {_dots::times}
      set {_request::timer} to 3

      send web request to "https://v1.api.lococovu.me/api/profile/%{_profile::token}%" and return as "%{_request}%"

    # Проверяем ответ удалённого сервера
    if {responses::%{_request}%::body} is set:
      delete {_body}
      delete {_response::*}

      set {_body} to {responses::%{_request}%::body}
      copy json {_body} to {_response::*}

      if {_response::error} isn't "NotFound":
        # И теперь давайте сохраним всю нужную
        # информацию про этот аккаунт.

        # Ну а теперь давайте же сохраним информацию
        # об этом аккаунте
        set {accounts::%{_profile::uid}%::email} to "%{_response::email}%"
        set {accounts::%{_profile::uid}%::nickname} to "%{_response::nickname}%"
        set {accounts::%{_profile::uid}%::displayName} to "%{_response::displayName}%"
        set {accounts::%{_profile::uid}%::level} to "%{_response::level::number}%"
        set {accounts::%{_profile::uid}%::lambdas} to "%{_response::wallet::lambdas}%"

        # И теперь давайте остановим этот воркер
        set {_working} to false

    add 0.05 to {_waiting::time}
    if {_waiting::time} >= 120:
      kick {_player} due to "&fЧувааак, случился какой-то лютый баг.%newline% Попробуй обратится к Администрации в нашем дс %newline% сервере &7(Вот тут )"
      stop

    # Визуальный таймер точек
    if {_dots::times} > 3:
      set {_dots::times} to 1

    # Таймер запроса
    if {_request::timer} <= 0:
      delete {_request::timer}
    else:
      reduce {_request::timer} by 0.05
    wait 1 second

  set {authorized::%{_player}%} to true
  # Берём информацию про аккаунт игрока.
  
  # Устанавливаем игровой скин
  set {_skin} to {characters::%{_profile::uid}%::0::skin}
  setSkin({_player}, {_skin})

  send title "&e☀ &fДобро пожаловать!" with subtitle "&7Мы действительно рады вас видеть." to {_player} for 4 seconds with 0 ticks fade in and 1 second fade out

  wait 2 seconds

  # TODO

  # Берём информацию про игровой
  # аккаунт и обрабатываем её
  # TODO: Сделать серверную синхронизацию
  # (синхронизацию через веб-ресурс)

  # Давайте сначала проверим давали ли мы игроку
  # дефолтный рюкзак с кошельком и деньгами.
    # Проверяем - получал ли игрок кошелёк с начальными деньгами
  # delete {temp::%{_profile::uid}%::receivedDefaultItems}
  if {temp::%{_profile::uid}%::receivedDefaultItems} is not set:
    set {temp::%{_profile::uid}%::receivedDefaultItems} to true

    # Подготавливаем кошелёк
    set {_wallet::random::1} to random integer between 0000 and 9999
    set {_wallet::random::2} to random integer between 0000 and 9999

    set {_wallet} to player head named "&e▨ &fКошелёк" with lore "&0%{_wallet::random::1}%-%{_wallet::random::2}%", "&f &fКошелёк - это именно то место, где хранятся", "&f &fвсе ваши сбережения и все ваши документы. Главное", "&f &fего не потерять, ибо потом придётся все документы", "&f &fсамому восстанавливать!", "", "&7С его внешним видом вам придётся смириться.", "&7Таков майнкрафт.", "", "&e▢ &fНажмите, что бы открыть"
    add "{SkullOwner:{Id:""ad2afd41-5cf0-4e11-bb8c-1a8253f1bfdb"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvOWRkNzUwMzEzYThjZmJjOTYxMTM5NmQ4Mjk5Y2QxMDgxNDZmN2JkMDllMjdlMTQwZTMyNGJjOWUyOThiZDY1YiJ9fX0=""}]}}}" to item-nbt of {_wallet}

    # И теперь давайте добавим начальные купюры в этот
    # кошелёк
    set {_id} to "%{_wallet::random::1}%-%{_wallet::random::2}%"

    set {_money::1} to GetMoneyBillItem(1)
    set {_money::5} to GetMoneyBillItem(5)
    set {_money::50} to GetMoneyBillItem(50)

    set {data::wallet::%{_id}%::inventory::0} to 5 of {_money::1}
    set {data::wallet::%{_id}%::inventory::1} to 2 of {_money::5}
    set {data::wallet::%{_id}%::inventory::2} to 1 of {_money::50}

    # Подготавливаем рюкзак
    set {_backpack::random::1} to random integer between 0000 and 9999
    set {_backpack::random::2} to random integer between 0000 and 9999
    
    set {_backpack} to player head named "&e▤ &fРюкзак" with lore "&0%{_backpack::random::1}%-%{_backpack::random::2}%", "&f &fРюкзак - это универсальное переносимое хранилище", "&f &fдля всех ваших предметов. Тут вы можете хранить", "&f &fдокументы, деньги, и подобное.", "", "&e▢ &fНажмите, что бы открыть"
    add "{SkullOwner:{Id:""5b699579-5444-a846-b7f9-dc0693b09586"",Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZWJkZjhkNTNiZGI5MzJjMjIzYzYyN2JiYjhjMWUwYzVlMzUxYTYxNmNkODA1NjkyOWM2NmU2ZGNlNDQ0MzNkYiJ9fX0=""}]}}}" to item-nbt of {_backpack}

    # И теперь давайте заполним этот рюкзак кое-чем
    set {_id} to "%{_backpack::random::1}%-%{_backpack::random::2}%"

    add {_wallet} to {data::backpack::%{_id}%::inventory::*}

    # Подготавливаем карту
    set {_random} to random integer between 10000 and 99999
    set {_card} to netherite ingot named "&e▬ &fКарта" with lore "&0%{_random}%", "&f &fКредитная карта - это отличный способ хранения денег.", "&f &fТут нет никаких лимитов или налогов, но оплавиваться", "&f &fкартой вы сможете не везде! Главное - это не потерять", "&f &fкарточку и позаботиться о защите и способах", "&f &fвосстановления карты. Всё это вы можете сделать или в самом", "&f &fтерминале или в отделении банка.", "", "&eⓘ &fКарточку нужно вставлять в &7Терминал"

    setCardBalance({_card}, 100)

    add {_card} to {data::backpack::%{_id}%::inventory::*}

    # Книга
    add {server::defaultBook} to {data::backpack::%{_id}%::inventory::*}

    # Выдаём рюкзак
    add {_backpack} to {_player}'s inventory

  # Просто телепортируем игрока в его квартирку

  wait 2 ticks
  TeleportToDefaultFlat({_player})