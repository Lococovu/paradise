function WR_Holo_MailboxCreate(player: player, location: location):
  # Сначала проверяем не стоит ли тут уже какая-то голограмма
  set {_hologram} to {hologram::%{_player}%::%{_location}%}

  if {_hologram} is set:
    if hologram {_hologram} is deleted:
    else:
      stop

  # Давайте же сразу создадим эту голограмму и
  # отдадим информацию о текущей голограмме обратно.
  delete {_items::*}

  set {_mailboxId} to {block::%{_location}%::mailboxId}

  set {_uuid} to "%{_player}'s uuid%-%getCurrentCharacter({_player})%"
  set {_items::*} to {data::mail::%{_mailboxId}%::%{_uuid}%::items::*}

  loop {_items::*}:
    delete {_arriveAfter}

    if {data::mail::item::%loop-value%::arrived} is not true:
      set {_arriveAfter} to {data::mail::item::%loop-value%::arriveAfter}
      
      {_arriveAfter} is set:
        {_arriveAfter} >= now:
          remove loop-value from {_items::*}
      else:
        remove loop-value from {_items::*}
    else:
      {data::mail::item::%loop-value%::picked} is true
      remove loop-value from {_items::*}

  if size of {_items::*} > 0:
    set {_size} to size of {_items::*}
    if {_size} is 1:
      set {_word} to "посылка"
    else:
      if {_size} > 4:
        set {_word} to "посылок"
      else:
        set {_word} to "посылки"

    set {_lines::items::*} to "&fВ почтовом ящике есть", "&9%size of {_items::*}% &f%{_word}% для вас", "&9▷ &fПКМ"
  else:
    set {_lines::items::*} to "&fПочтовый ящик пустой"

  set {_size::initial} to size of {_items::*}
  set {_lines::*} to "", "", "&7&lПочтовый ящик", {_lines::items::*}

  set {_hologram::location} to {_location}
  add 1.5 to y coordinate of {_hologram::location}

  if distance between {_player} and {_hologram::location} >= 3.5:
    stop
  create new hologram with clickable lines {_lines::*} at {_hologram::location} and store it in variable {_hologram}

  make hologram {_hologram} invisible by default
  reveal hologram {_hologram} to {_player}

  # Ну а теперь давайте создадим небольшой воркер, который будет
  # проверять релевантность этой голограммы.
  set {_waiting} to true

  while {_waiting} is true:
    if {_player} is offline:
      delete {hologram::%{_player}%::%{_location}%}
      delete hologram {_hologram}
      stop

    if {_action} is "update":
      delete {hologram::%{_player}%::%{_location}%}
      delete hologram {_hologram}

      WR_Holo_MailboxCreate({_player}, {_location})

      stop
    
    # Проверяем состояние текущего блока
    if distance between {_player} and {_hologram::location} >= 3.5:
      delete {hologram::%{_player}%::%{_location}%}
      delete hologram {_hologram}
      stop

    if {hologram::%{_player}%::%{_location}%} isn't {_hologram}:
      set {hologram::%{_player}%::%{_location}%} to {_hologram}

    if {_update::timer} <= 0:
      set {_update::timer} to 1
      set {_items::*} to {data::mail::%{_mailboxId}%::%{_uuid}%::items::*}

      loop {_items::*}:
        delete {_arriveAfter}
        
        if {data::mail::item::%loop-value%::arrived} is not true:
          set {_arriveAfter} to {data::mail::item::%loop-value%::arriveAfter}
          
          {_arriveAfter} is set:
            {_arriveAfter} >= now:
              remove loop-value from {_items::*}
          else:
            remove loop-value from {_items::*}
        else:
          {data::mail::item::%loop-value%::picked} is true
          remove loop-value from {_items::*}

      if size of {_items::*} is not {_size::initial}:
        set {_action} to "update"

    reduce {_update::timer} by 0.05
    wait 1 tick

function WR_Holo_MailboxClick(player: player):
  # Берём ид почтовой коробки
  loop all blocks in radius 3 around {_player}:
    set {_location::block} to location of block at loop-value
    set {_mailboxId} to {block::%{_location::block}%::mailboxId}

    {_mailboxId} is set:
      exit 1 loop

  # Открываем меню
  {_mailboxId} is set
  MailModule_OpenMailboxMenu({_player}, {_mailboxId})